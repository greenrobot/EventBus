apply plugin: 'java'

archivesBaseName = 'eventbus'
group = 'org.greenrobot'
version = '3.2.0'
sourceCompatibility = 1.7

dependencies {
    compileOnly 'com.google.android:android:4.1.1.4'
    compileOnly 'com.google.android:android-test:4.1.1.4'
    compileOnly 'com.google.android:annotations:4.1.1.4'
    compileOnly 'com.google.android:support-v4:r7'
}

sourceSets {
    main {
        java {
            srcDir 'src'
            // exclude 'de/greenrobot/event/util/**'
        }
    }
}

apply from: rootProject.file("gradle/publish.gradle")

javadoc {
    failOnError = false
    title = "EventBus ${version} API"
	options.bottom = 'Available under the Apache License, Version 2.0 - <i>Copyright &#169; 2012-2020 <a href="https://greenrobot.org">greenrobot.org</a>. All Rights Reserved.</i>'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            // Common setup is defined in publish.gradle.

            pom.project {
               name 'EventBus'
               description 'EventBus is a publish/subscribe event bus optimized for Android.'
           }
        }
    }
}
