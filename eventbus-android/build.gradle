buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.0.2'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group = rootProject.group
version = rootProject.version

android {
    namespace "org.greenrobot.eventbus.android"
    compileSdkVersion = _compileSdkVersion

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 33 // Android 11 (R)

        consumerProguardFiles "consumer-rules.pro"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

dependencies {
    api project(":eventbus-java")
}

// Set project-specific properties
// https://developer.android.com/studio/build/maven-publish-plugin
// Because the Android components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = "eventbus"

                from components.release

                pom {
                    name = "EventBus"
                    description = "EventBus is a publish/subscribe event bus optimized for Android."
                }
            }
        }
    }
}
